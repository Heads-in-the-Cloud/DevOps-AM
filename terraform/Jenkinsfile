pipeline {
    agent {
        node {
            label 'aws-ready'
            customWorkspace "${AM_DEVOPS_DIRECTORY}"
        }
    }

    environment {
        COMMIT_HASH     = sh(returnStdout: true, script: "git rev-parse --short=8 HEAD").trim()
        SECRET_ID       = "dev/AM/utopia-secrets"
        SECRET_ID_PUSH  = "dev/AM/utopia-secrets-NE4x9z"

        // Terraform passthrough
        TF_VAR_ECS_RECORD     = "${AM_ECS_RECORD_NAME}"
        TF_VAR_EKS_RECORD     = "${AM_EKS_RECORD_NAME}"
        TF_VAR_HOSTED_ZONE    = "${AWS_HOSTED_ZONE_ID}"
        TF_VAR_REGION_ID      = "${AWS_REGION_ID}"
        TF_VAR_AWS_ACCESS_KEY = credentials('AM_AWS_ACCESS')
        TF_VAR_AWS_SECRET_KEY = credentials('AM_AWS_SECRET')
        TF_VAR_AWS_SECRET_ID  = "${AM_SECRET_ID}"
        TF_VAR_AZ_1           = "${AWS_REGION_AZ_1}"
        TF_VAR_AZ_2           = "${AWS_REGION_AZ_2}"
    }

    parameters {
        booleanParam(name: "APPLY", defaultValue: false)
    }

    stages {

        stage('Load Environment') {
            steps {
                echo 'Loading Environment'
                script {
                    withCredentials([
                        string(credentialsId: SECRET_ID,
                        variable: 'DB_CREDS')
                    ]) {
                        def jsonObj = readJSON text: DB_CREDS
                        env.TF_VAR_DB_USERNAME    = jsonObj.DB_USERNAME
                        env.TF_VAR_DB_PASSWORD    = jsonObj.DB_PASSWORD
                    }
                }
            }
        }

        stage('Terraform Plan') {
            steps {
                echo 'Planning terraform infrastructure'
                dir("terraform") {
                    sh 'mkdir -p plans'
                    sh 'terraform init -no-color'
                    sh 'terraform plan -out plans/plan-${COMMIT_HASH}.tf -no-color > plans/plan-${COMMIT_HASH}.txt'
                }
            }
        }

        stage('Terraform Apply') {
            when { expression { params.APPLY } }
            steps {
                echo 'Applying Terraform objects'
                dir("terraform") {
                    sh 'terraform apply -no-color -auto-approve plans/plan-${COMMIT_HASH}.tf'
                }
            }
        }

        stage('Terraform Output') {
            when { expression { params.APPLY } }
            steps {
                echo 'Exporting outputs as secrets'
                dir("terraform") {
                    sh 'terraform refresh'
                    sh 'terraform output | tr -d \'\\\"\\ \' > env.txt'
                }
            }
        }

        stage('Update Secrets') {
            when { expression { params.APPLY } }
            steps {
                echo 'Writing output to Secrets'
                script {
                    withCredentials([
                        string (credentialsId: env.SECRET_ID,
                        variable: 'DB_CREDS')
                    ]) {
                        // json objects
                        def creds = readJSON text: DB_CREDS
                        def outputs = readProperties file: 'terraform/env.txt'

                        // secret keys
                        creds.AWS_VPC_ID          = outputs.AWS_VPC_ID
                        creds.AWS_RDS_ENDPOINT    = outputs.AWS_RDS_ENDPOINT
                        creds.AWS_ALB_ID          = outputs.AWS_ALB_ID

                        // rewrite secret
                        String jsonOut = writeJSON returnText: true, json: creds
                        sh "aws secretsmanager update-secret --secret-id 'arn:aws:secretsmanager:us-west-2:${AWS_ACCOUNT_ID}:secret:${SECRET_ID_PUSH}' --region us-west-2 --secret-string '${jsonOut}'"
                    }
                }
            }
        }

        stage('Cleanup') {
            when { expression { params.APPLY } }
            steps {
                echo 'Removing temp files'
                sh 'rm -f terraform/env.txt'
            }
        }

        // end stages
    }
}
